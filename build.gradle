plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.spotbugs' version '5.0.7'
    id 'pmd'
    id 'checkstyle'
    id 'maven-publish'
    id 'signing'
}

group 'com.levelrin'
version '0.1.0'

// Source compatibility is Java 8 on purpose because many people are still using Java 8.
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'net.jcip:jcip-annotations:1.0'
    implementation 'com.github.spotbugs:spotbugs-annotations:4.6.0'
    implementation 'org.jetbrains:annotations:23.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.mockito:mockito-core:4.6.1'
}

test {
    useJUnitPlatform()
    dependsOn 'cleanTest'
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat = 'full'
    }
}

jacoco {
    toolVersion = '0.8.7'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                enabled = true
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.00000
            }
            limit {
                enabled = true
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.00000
            }
            limit {
                enabled = true
                counter = 'COMPLEXITY'
                value = 'COVEREDRATIO'
                minimum = 0.00000
            }
            limit {
                enabled = true
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.00000
            }
            limit {
                enabled = true
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.00000
            }
            limit {
                enabled = true
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 0.00000
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

check.dependsOn jacocoTestCoverageVerification
check.dependsOn jacocoTestReport

spotbugs {
    toolVersion = '4.7.0'
    ignoreFailures = false
    effort = 'max'
    excludeFilter = file('config/spotbugs/exclude.xml')
}
spotbugsMain {
    reports {
        html {
            required = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}
spotbugsTest {
    reports {
        html {
            required = true
            destination = file("$buildDir/reports/spotbugs/test/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

pmd {
    toolVersion = '6.46.0'
    ignoreFailures = false
    ruleSetFiles = files('config/pmd/ruleSet.xml')
    ruleSets = []
}
tasks.withType(Pmd) {
    reports {
        xml.required = true
        html.required = true
    }
}

checkstyle {
    // 9.3 is the latest version that supports Java 8.
    toolVersion '9.3'
    ignoreFailures = false
    configFile = file('config/checkstyle/checkstyle.xml')
    configProperties = [
        'checkstyle.header.file': file('config/checkstyle/java.header'),
        'checkstyle.suppression.file': file('config/checkstyle/suppression.xml')
    ]
}
tasks.withType(Checkstyle) {
    reports {
        xml.required = true
        html.required = true
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'chocomint-httpclient'
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'chocomint-httpclient'
                description = 'An HTTP client in Java that follows the ChocoMint principle.'
                url = 'https://github.com/levelrin/chocomint-httpclient'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/levelrin/chocomint-httpclient/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'levelrin'
                        name = 'Rin'
                        email = 'levelrin@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:levelrin/chocomint-httpclient.git'
                    developerConnection = 'scm:git:git@github.com:levelrin/chocomint-httpclient.git'
                    url = 'https://github.com/levelrin/chocomint-httpclient'
                }
            }

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }

    repositories {
        maven {
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            credentials {
                def ossrhUser = project.hasProperty('ossrhUser') ? ossrhUser : 'fakeUser'
                def ossrhPassword = project.hasProperty('ossrhPassword') ? ossrhPassword : 'fakePassword'

                username ossrhUser
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
